

provide:
   tests
   TestMask
   Test
   TestSuite
   formatTests

   
global.___letest_global = exports

tests{ctx, scope, form, #data{descr, _body}} =

   letest = #variable{"___letest_global"}

   parseDescription{match d} =
      #value{String! s} ->
         tags = [s.match{R.g"#[a-zA-Z0-9_-]+"} or {}] each
            tag -> #value{tag.slice{1}}
         {d, `{^*tags}`}
      #void{} ->
         {`""`, `{}`}
      else ->
         throw E.tests.description{"Invalid description."}

   processSuite{#multi! #multi{*stmts}} =

      {let body, let tests} = match stmts:
         {`do: ^[#multi! #multi{*b}]`, *rest} ->
            {#multi{*b}, rest}
         xs ->
            {#multi{}, xs}

      var index = 0
      let gene = #data{}
      let descrs = #data{}

      tests each
         `do: ^_` ->
            throw E.letest{"do: statement must be at the beginning of the block."}
         `^descr => ^b` ->
            {txt, tags} = parseDescription{descr}
            {g, s} = processSuite{b}
            gene.push with g
            descrs.push with `#suite{^txt, ^tags, ^s}`
         expr ->
            gene.push with `async{{} -> ^expr}`
            descrs.push with `#test{^=index, null, null}`
            index += 1

      body.push{gene}
      {`async{{} -> ^body}`, descrs}

   {v, description, body} = match descr:
      v and #symbol{name} ->
         {v, #value{"#" + name}, _body}
      #send{v and #symbol{name}, `{^ #value{descr}}` or #value{descr}} ->
         {v, #value{String{descr} + " #" + name}, _body}

   {gene, trees} = processSuite{body}
   {d, t} = parseDescription{description}

   `[^v = [^letest].TestSuite{#suite{^d, ^t, ^trees}, ^gene}]`


class TestSuite:

   constructor{@descr, @gen} =
      pass

   select{white = true, black = {}} =
      trim{arr} =
         var i = arr.length - 1
         while i > 0 and arr[i] === false:
            arr.pop{}
            i -= 1
         arr

      {_, text, tags, subdescr} = @descr
      var count = 0
      mask = helper{tags, subdescr, false} where
         helper{t, sd, default} =
            if sd === null:
               if default: count += 1
               default
            elif matchtags{t, black}:
               false
            elif white === true or matchtags{t, white}:
               res = sd each {_, _, t, sd} -> helper{t, sd, true}
               res.every{{x} -> x === true} or trim{res}
            else:
               res = sd each {_, _, t, sd} -> helper{t, sd, default}
               res.every{{x} -> x === true} or [res.some{{x} -> x} and trim{res}]

      TestMask{@descr, @gen, mask, count}

   execute{} =
      @select{true}.execute{}


class Test:
   constructor{@test, @source} =
      pass
   async! execute{} =
      try:
         match [await @test{}]:
            === true ->
               #success{}
            === false ->
               #failure{}
            res and [#success or #failure] ->
               res
            other ->
               #error{E.tests.protocol{..., other}} with
                  "Tests must return true, false, #success{...} or #failure{...}"
      catch error:
         #error{error}


matchtags{tags1, tags2} =
   tags1 each t1 ->
      tags2 each t2 ->
         if t1 == t2:
            return true
      pass
   false


class TestMask:

   constructor{@descr, @gen, @mask, @count = 0} =
      pass

   async! getTests{var path = {}} =
      match @descr:
         when @mask === false ->
            pass
         #test{*} ->
            {#test{path, Test{@gen, null}}}
         #suite{d, t, sub} ->
            if Number? path[path.length - 1]:
               path[path.length - 1] = d
            else:
               path ++= {d}
            var pos = 0
            var results = {}
            try:
               tests = await @gen{}
               tests each test ->
                  match if{@mask === true, @mask, @mask[pos]}:
                     undefined? ->
                        break
                     false? ->
                        pass
                     submask ->
                        m = TestMask{sub[pos], test, submask}
                        newpath = if{sub.length == 1, path, path ++ {pos}}
                        results ++= [await m.getTests{newpath}]
                  pos += 1
            catch error:
               results.push with #error{path, error, tests.length - pos}
            results

   async! getResults{} =
      [await @getTests{}] each match test ->
         #test{path, t} ->
            #testResult{path, t, await t.execute{}}
         #error ->
            test


async! formatTests{tsts} =
   error_reports = {}

   var successes = 0
   var failures = 0
   var errors = 0

   stream = process.stdout
   tick{} =
      stream.clearLine{}
      stream.cursorTo{0}
      stream.write{"Successes: " + successes + ", Failures: " + failures + ", Errors: " + errors}

   fmtp{path} =
      [p.join{" => "}] where p = path each
         Number? n -> "Test #" + n
         s -> s

   alltests = await tsts.getResults{}

   for test of alltests:
      match test:
         #testResult{path, test, x} ->
            match [await x]:
               #success ->
                  successes += 1
                  tick{}
               #failure ->
                  failures += 1
                  tick{}
                  var report = "FAILURE: " + fmtp{path} + "\n"
                  report += "SOURCE: " + test.source + "\n"
                  error_reports.push with report
               #error{error} ->
                  errors += 1
                  tick{}
                  var report = "ERROR: " + fmtp{path} + "\n"
                  report += "SOURCE: " + test.source + "\n"
                  report += String{error} + "\n"
                  error_reports.push with report
         #error{path, suite, error, count} ->
            errors += count
            tick{}
            var report = "ERROR: " + fmtp{path} + "\n"
            report += count + " tests could not be run because of this error." + "\n"
            error_reports.push with report

   console.log{}

   error_reports each report ->
      console.log with "\n==========="
      console.log with report

   console.log with "Done."

   pass
